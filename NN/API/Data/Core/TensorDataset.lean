/-
Copyright (c) 2026 TorchLean
Released under MIT license as described in the file LICENSE.
Authors: TorchLean Team
-/

module

public import NN.API.Data.Core.Dataset

/-!
# Tensor Datasets

Conversion between leading-axis tensors, tensor packs, and runtime datasets.
-/

@[expose] public section

namespace NN
namespace API
namespace Data

/-!
## TensorDataset (leading-axis batching)

PyTorch's `TensorDataset` concept is: given one or more tensors that share the same `size(0)`,
build a dataset of samples by slicing each tensor along its leading batch axis.

In TorchLean we do the same thing, but with shapes tracked in the type:

- a batched tensor has shape `.dim n σ`,
- slicing at `i : Fin n` yields a sample of shape `σ`,
- and a batch of multiple tensors is represented as a `TensorPack`.
-/

/--
Slice a batched `TensorPack` along its leading batch axis.

If a sample is represented as a shape-indexed tuple `TensorPack β ss`, then a minibatch of size `n`
is `TensorPack β (ss.map (fun s => .dim n s))`. This function picks a batch index `i : Fin n` and returns
the corresponding single sample.
-/
def unbatchTensorPack {β : Type} {n : Nat} :
    {ss : List Spec.Shape} →
      API.TorchLean.TensorPack β (ss.map (fun s => Spec.Shape.dim n s)) →
      Fin n →
      API.TorchLean.TensorPack β ss
  | [], .nil, _i => .nil
  | _s :: ss, .cons x xs, i =>
      .cons (Spec.getAtSpec x i) (unbatchTensorPack (β := β) (ss := ss) xs i)

/-- Convert a shape-indexed `TensorPack` of `Float` tensors to the runtime scalar type `α`. -/
def castTListOfFloat {α : Type} [API.Runtime.Scalar α] :
    {ss : List Spec.Shape} →
      API.TorchLean.TensorPack Float ss →
      API.TorchLean.TensorPack α ss
  | [], .nil => .nil
  | _s :: ss, .cons x xs =>
      .cons (Spec.mapTensor (API.Runtime.ofFloat (α := α)) x) (castTListOfFloat (ss := ss) xs)

/--
Build a dataset by slicing a *batched* `TensorPack` along the leading batch axis. This gives the
typed counterpart of a tensor dataset built from several aligned arrays.
-/
def tensorDatasetFromLeadingAxis {β : Type} {n : Nat} {ss : List Spec.Shape}
    (xs : API.TorchLean.TensorPack β (ss.map (fun s => Spec.Shape.dim n s))) :
    Dataset (API.TorchLean.TensorPack β ss) :=
  fromList <| (List.finRange n).map (fun i => unbatchTensorPack (β := β) (n := n) (ss := ss) xs i)

/--
Float-to-`α` variant of `tensorDatasetFromLeadingAxis`, for data loaded from disk.
-/
def tensorDatasetFromLeadingAxisFloat {α : Type} [API.Runtime.Scalar α]
    {n : Nat} {ss : List Spec.Shape}
    (xs : API.TorchLean.TensorPack Float (ss.map (fun s => Spec.Shape.dim n s))) :
    Dataset (API.TorchLean.TensorPack α ss) :=
  let samples : List (API.TorchLean.TensorPack α ss) :=
    (List.finRange n).map (fun i =>
      castTListOfFloat (α := α) (unbatchTensorPack (β := Float) (n := n) (ss := ss) xs i))
  fromList samples

/--
Supervised dataset from two batched tensors `X : (n, σ)` and `Y : (n, τ)` by slicing the leading batch axis.

This is the common regression/supervised-learning case: the TorchLean analogue of
`TensorDataset(X, Y)` in PyTorch.
-/
def supervisedFromLeadingAxis {α : Type}
    {n : Nat} {σ τ : Spec.Shape}
    (X : Spec.Tensor α (.dim n σ))
    (Y : Spec.Tensor α (.dim n τ)) :
    Dataset (API.TorchLean.TensorPack α [σ, τ]) :=
  tensorDatasetFromLeadingAxis (β := α) (n := n) (ss := [σ, τ])
    (tensorpack! X, Y)

/-- Float-to-`α` variant of `supervisedFromLeadingAxis`, for data loaded from disk. -/
def supervisedFromLeadingAxisFloat {α : Type} [API.Runtime.Scalar α]
    {n : Nat} {σ τ : Spec.Shape}
    (X : Spec.Tensor Float (.dim n σ))
    (Y : Spec.Tensor Float (.dim n τ)) :
    Dataset (API.TorchLean.TensorPack α [σ, τ]) :=
  tensorDatasetFromLeadingAxisFloat (α := α) (n := n) (ss := [σ, τ])
    (tensorpack! X, Y)

end Data
end API
end NN
