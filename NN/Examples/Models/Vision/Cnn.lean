/-
Copyright (c) 2026 TorchLean
Released under MIT license as described in the file LICENSE.
Authors: TorchLean Team

Device-agnostic real-data example:
  python3 scripts/datasets/download_example_data.py --cifar10
  lake exe torchlean cnn --cpu
  lake build -R -K cuda=true && lake exe torchlean cnn --cuda
-/

module


public import NN
public import NN.API.Models.Cnn
public import NN.Examples.Models.Common.RealData
public import LeanProfiler

/-!
# CNN Training Example

Runnable `torchlean cnn` example. It trains a small convolutional classifier on a prepared CIFAR-10
minibatch.

The reusable model wiring lives in `NN.API.Models.Cnn` (`nn.models.cnn`). This file is the
runnable wrapper: command-line parsing, dataset selection, step-limited loader training, and
TrainLog artifact writing.

```bash
python3 scripts/datasets/download_example_data.py --cifar10
  lake build -R -K cuda=true && lake exe torchlean cnn --cuda --steps 1

LeanProfiler: set `leanProfilerEnabled := true` below, rebuild, then run a short job.
Profile output: `data/profiles/cnn.json` (open at ui.perfetto.dev).
```
-/

@[expose] public section

open Spec Tensor
open NN.API

namespace NN.Examples.Models.Vision.Cnn

/-- Set to `true` and rebuild to profile this example with LeanProfiler. -/
def leanProfilerEnabled : Bool := true

def profileOut : System.FilePath := "data/profiles/cnn.json"

def exeName : String := "torchlean cnn"
def defaultLogJson : System.FilePath := "data/model_zoo/cnn_trainlog.json"

def batch : Nat := 4
def inC : Nat := 3
def inH : Nat := RealData.cifarHeight
def inW : Nat := RealData.cifarWidth

def outDim : Nat := RealData.cifarClasses

def cfg : nn.models.CnnConfig :=
  { batch := batch, inC := inC, inH := inH, inW := inW, outDim := outDim }

abbrev σ : Shape :=
  nn.models.cnnInShape cfg

abbrev τ : Shape :=
  nn.models.cnnOutShape cfg

def mkModel : nn.M (nn.Sequential σ τ) :=
  nn.models.cnn cfg

set_option profiler.instrument true

def loadCifarLoader {α : Type} [Semantics.Scalar α] [Runtime.Scalar α]
    (xPath yPath : System.FilePath) (nRows seed : Nat) :
    IO (Data.BatchLoader α batch RealData.CifarImage RealData.CifarTarget) := do
  RealData.loadCifarLoader (α := α) exeName batch nRows seed xPath yPath

/--
Train the CNN on the prepared CIFAR loader using the standard Float runtime training path.

The model-specific part is only the loss module: CIFAR labels are one-hot vectors, so we use
cross-entropy with mean reduction.  The generic pieces -- Adam, step-counted loader training,
before/after reports, optional CUDA telemetry, and TrainLog JSON -- are handled by the public
`train.fitModuleLoaderStepsLoggedFloat` helper.
-/
def fitCifar (opts : Runtime.Autograd.Torch.Options)
    (xPath yPath : System.FilePath) (nRows seed : Nat) (trainCfg : Common.ModelTrainFlags) :
    IO (train.FitReport Float) := do
  let loader ← loadCifarLoader (α := Float) xPath yPath nRows seed
  nn.withModel mkModel fun model => do
    let modDef := nn.crossEntropyOneHotScalarModuleDef model (reduction := .mean)
    let module ← TorchLean.Module.instantiateWithOptions (α := Float) modDef id opts
    let opt := Common.adamOptimizer (α := Float) id (nn.paramShapes model) trainCfg.lr
    let cudaMemWatch :=
      Common.effectiveCudaMemWatch opts trainCfg.train.steps trainCfg.cudaMemWatch
    let (report, _loader') ← train.fitModuleLoaderStepsLoggedFloat module opt opts
      trainCfg.train.steps loader trainCfg.train.log "CNN training"
      #[s!"data=cifar10", s!"x={xPath}", s!"y={yPath}", s!"nRows={nRows}",
        s!"device={if opts.useGpu then "cuda" else "cpu"}", s!"lr={trainCfg.lr}",
        s!"steps={trainCfg.train.steps}", s!"batch={batch}", s!"cuda_mem_watch={cudaMemWatch}"]
      "loss" trainCfg.cudaMemWatch
    pure report

set_option profiler.instrument false

def main (args : List String) : IO UInt32 := do
  Common.runFloat exeName args
    (banner := fun opts =>
      s!"{exeName}: CNN training (device={if opts.useGpu then "cuda" else "cpu"})")
    (k := fun opts rest => do
      let (xPath, yPath, nRows, seed, rest) ← Common.orThrow exeName <|
        RealData.parseCifarFlags rest
      let (trainCfg, rest) ← Common.orThrow exeName <|
        Common.parseModelTrainFlags exeName rest defaultLogJson 1 1e-3
      Common.orThrow exeName <| CLI.requireNoArgs rest
      let report ← fitCifar opts xPath yPath nRows seed trainCfg
      if leanProfilerEnabled then do
        IO.println "=== LeanProfiler summary ==="
        printSummary
        exportProfile profileOut
        IO.println s!"  wrote profile: {profileOut}"
      IO.println s!"  steps={trainCfg.train.steps} loss0={report.before} loss1={report.after}")

end NN.Examples.Models.Vision.Cnn
