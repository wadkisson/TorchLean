/-
Copyright (c) 2026 TorchLean
Released under MIT license as described in the file LICENSE.
Authors: TorchLean Team
-/

module

public import NN.Runtime.Autograd.Utils
public import NN.Spec.Models.Mlp
public import NN.Tensor

/-!
# AutogradEngineTest

Regression tests for `Runtime.Autograd` dynamic tape over `ℚ`.

We check that for a simple 2-layer MLP, the tape-based gradients match the existing
hand-derived `Examples.mlp_backward`.
-/

open scoped NN.Spec.RationalAlgebraic

@[expose] public section


open Spec
open Tensor
open Examples

namespace Tests
namespace Rationals
namespace AutogradEngine

open Runtime.Autograd

abbrev inDim  := 2
abbrev hidDim := 3
abbrev outDim := 1

-- Small tag used for readable error messages.
abbrev tag : String := "autograd_engine_test (Rat)"

-- Parameter node ids we want to read gradients for.
structure ParamIds where
  /-- w 1 Id. -/
  hiddenWeightId : Nat
  /-- b 1 Id. -/
  hiddenBiasId : Nat
  /-- w 2 Id. -/
  outputWeightId : Nat
  /-- b 2 Id. -/
  outputBiasId : Nat

/-!
## Fixed inputs and parameters

We use a small deterministic 2-layer MLP so the gradients are stable.
-/
def hiddenWeight : Tensor ℚ (.dim hidDim (.dim inDim .scalar)) :=
  tensorOfList! [hidDim, inDim] [0.1, 0.2, 0.3, 0.4, 0.5, 0.6]

def hiddenBias : Tensor ℚ (.dim hidDim .scalar) :=
  tensorOfList! [hidDim] [0.1, 0.2, 0.3]

def outputWeight : Tensor ℚ (.dim outDim (.dim hidDim .scalar)) :=
  tensorOfList! [outDim, hidDim] [0.7, 0.8, 0.9]

def outputBias : Tensor ℚ (.dim outDim .scalar) :=
  tensorOfList! [outDim] [0.4]

def x : Tensor ℚ (.dim inDim .scalar) :=
  tensorOfList! [inDim] [0.5, 0.8]

def dLdy : Tensor ℚ (.dim outDim .scalar) :=
  tensorOfList! [outDim] [1.0]

def hiddenLayer : Spec.LinearSpec ℚ inDim hidDim := { weights := hiddenWeight, bias := hiddenBias }
def outputLayer : Spec.LinearSpec ℚ hidDim outDim := { weights := outputWeight, bias := outputBias }

def expected :=
  Examples.mlpBackward hiddenLayer outputLayer x dLdy

/-!
## Test: dynamic tape gradients vs. reference

We compare the autograd tape gradients against the hand-derived MLP backward pass.
-/
def checkMlpGrads :
  Runtime.Autograd.Result Bool := do
  let t0 : Tape ℚ := Tape.empty

  -- Build the graph in TapeM for readability.
  let m : TapeM ℚ _ := do
    let hiddenWeightId ← Train.TapeM.param hiddenWeight (name := some "hiddenWeight")
    let hiddenBiasId ← Train.TapeM.param hiddenBias (name := some "hiddenBias")
    let outputWeightId ← Train.TapeM.param outputWeight (name := some "outputWeight")
    let outputBiasId ← Train.TapeM.param outputBias (name := some "outputBias")
    let xId ← Train.TapeM.const x (name := some "x")

    -- Forward pass: linear -> relu -> linear
    let z1Id ← TapeM.linear (inDim:=inDim) (outDim:=hidDim) hiddenWeightId hiddenBiasId xId
    let a1Id ← TapeM.relu (s:=.dim hidDim .scalar) z1Id
    let yId ← TapeM.linear (inDim:=hidDim) (outDim:=outDim) outputWeightId outputBiasId a1Id

    let t ← TapeM.getTape
    let grads ← liftM (Tape.backward (t:=t) yId (Runtime.Autograd.AnyTensor.mk dLdy))

    let ids : ParamIds := { hiddenWeightId := hiddenWeightId, hiddenBiasId := hiddenBiasId, outputWeightId := outputWeightId, outputBiasId := outputBiasId }
    pure (ids, grads)

  let ((ids, grads), _) ← TapeM.run t0 m

  let (dW1_exp, db1_exp, dW2_exp, db2_exp, _dX_exp) := expected

  let dW1_dyn ← Train.requireGradTensor (tag := tag)
    (s:=.dim hidDim (.dim inDim .scalar)) grads ids.hiddenWeightId
  let db1_dyn ← Train.requireGradTensor (tag := tag)
    (s:=.dim hidDim .scalar) grads ids.hiddenBiasId
  let dW2_dyn ← Train.requireGradTensor (tag := tag)
    (s:=.dim outDim (.dim hidDim .scalar)) grads ids.outputWeightId
  let db2_dyn ← Train.requireGradTensor (tag := tag)
    (s:=.dim outDim .scalar) grads ids.outputBiasId

  let ok1 := decide (pretty dW1_dyn = pretty dW1_exp)
  let ok2 := decide (pretty db1_dyn = pretty db1_exp)
  let ok3 := decide (pretty dW2_dyn = pretty dW2_exp)
  let ok4 := decide (pretty db2_dyn = pretty db2_exp)
  pure (ok1 && ok2 && ok3 && ok4)

def run : IO Unit := do
  match checkMlpGrads with
  | .ok true => IO.println "autograd_engine_test (Rat): OK"
  | .ok false => throw <| IO.userError "autograd_engine_test (Rat): FAILED"
  | .error msg => throw <| IO.userError s!"autograd_engine_test (Rat): {msg}"

end AutogradEngine
end Rationals
end Tests
